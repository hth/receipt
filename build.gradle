if (!hasProperty('env')) {
    env = System.env['GRADLE_ENV']
}

ext {
    webInf = file('/WEB-INF')
    lib = file("$webInf/lib")
    classes = file("$webInf/classes")
    conf = file("$classes/conf")
    images = file("$classes/images")

    warDir = file("$buildDir/explodedWar/$env")
    warWebInfDir = file("$warDir$webInf")
    warLibDir = file("$warDir$lib")
    warClassesDir = file("$warDir$classes")
    warConfDir = file("$warDir$conf")
    warWebInfClassesImagesDir = file("$warDir$images")

    unzipDir = file("${buildDir}/unpacked/dist")
    unzipLibDir = file("$unzipDir$lib")

    passFile = file('/Users/Shared/Jenkins/Documents/receiptofi-additional-files/pass.properties')
    passFileLocal = file('web/resources/conf/pass.properties')
}

allprojects {
    group = 'com.receiptofi'
    version = '0.9.0'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'checkstyle'  //for java code conventions, you can use sonar instead but it need DB
    apply plugin: 'findbugs'    //for static code analysis
    apply plugin: 'jdepend'     //for dependency analysis
    apply plugin: 'pmd'
    apply plugin: 'jacoco'      //for code coverage

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    pmd {
        ignoreFailures = false
    }

    checkstyle {
        ignoreFailures = true
    }

    findbugs {
        toolVersion = "3.0.0"
        ignoreFailures = true
        reportsDir = file("$project.buildDir/reports/findbugs")
        effort = "max"
        reportLevel = "medium"
        visitors = ["FindSqlInjection", "SwitchFallthrough"]
        omitVisitors = ["FindNonShortCircuit"]
//        includeFilter = file("$rootProject.projectDir/config/findbugs/includeFilter.xml")
//        excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
    }

    jar {
        manifest {
            attributes(
                    "Implementation-Vendor": "Receiptofi Inc",
                    "Implementation-Title": rootProject.name,
                    "Implementation-Version": allprojects.version + " $env",
                    "Copyright" : "2014, Receiptofi Inc"
            )
        }
    }

    idea {
        module {
            downloadSources = true
            downloadJavadoc = false
        }
    }

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/libs-milestone" }
        maven { url "http://repo1.maven.org/maven2/" }
    }

    dependencies {
        compile files('../lib/spring-social-core-1.1.0.RELEASE.jar')
        compile files('../lib/spring-social-facebook-1.1.1.RELEASE.jar')
        compile files('../lib/spring-social-google-1.0.0.RELEASE.jar')
        compile files('../lib/spring-social-security-1.1.0.RELEASE.jar')
        compile files('../lib/spring-social-web-1.1.0.RELEASE.jar')
        compile files('../lib/geronimo-j2ee-management_1.1_spec-1.0.1.jar')
        compile files('../lib/geronimo-jms_1.1_spec-1.1.1.jar')

        compile 'org.springframework:spring-aop:3.2.8.RELEASE'
        compile 'org.springframework:spring-aspects:3.2.9.RELEASE'
        compile 'org.springframework:spring-beans:3.2.9.RELEASE'
        compile 'org.springframework:spring-context:3.2.9.RELEASE'
        compile 'org.springframework:spring-context-support:3.2.9.RELEASE'
        compile 'org.springframework:spring-core:3.2.9.RELEASE'
        compile 'org.springframework:spring-expression:3.2.9.RELEASE'
        compile 'org.springframework:spring-instrument:3.2.9.RELEASE'
        compile 'org.springframework:spring-instrument-tomcat:3.2.9.RELEASE'
        compile 'org.springframework:spring-jms:3.2.9.RELEASE'
        compile 'org.springframework:spring-oxm:3.2.9.RELEASE'
        compile 'org.springframework:spring-test:3.2.9.RELEASE'
        compile 'org.springframework:spring-tx:3.2.9.RELEASE'
        compile 'org.springframework:spring-web:3.2.9.RELEASE'
        compile 'org.springframework:spring-webmvc:3.2.9.RELEASE'

        compile 'org.springframework.security:spring-security-aspects:3.2.4.RELEASE'
        compile 'org.springframework.security:spring-security-config:3.2.4.RELEASE'
        compile 'org.springframework.security:spring-security-core:3.2.4.RELEASE'
        compile 'org.springframework.security:spring-security-crypto:3.2.4.RELEASE'
        compile 'org.springframework.security:spring-security-taglibs:3.2.4.RELEASE'
        compile 'org.springframework.security:spring-security-web:3.2.4.RELEASE'

        //TODO remove or use
        compile 'org.springframework.data:spring-data-redis:1.3.2.RELEASE'
        compile 'redis.clients:jedis:2.5.1'

        compile 'org.springframework.data:spring-data-mongodb:1.5.0.RELEASE'
        compile 'org.springframework.data:spring-data-commons:1.8.0.RELEASE'
        compile 'org.mongodb:mongo-java-driver:2.12.2'

        compile 'org.apache.commons:commons-collections4:4.0'
        compile 'org.apache.commons:commons-lang3:3.3.2'

        compile 'aopalliance:aopalliance:1.0'
        compile 'org.aspectj:aspectjrt:1.7.4'
        compile 'com.fasterxml:classmate:1.0.0'
        compile 'org.slf4j:slf4j-api:1.7.7'
        compile 'org.hibernate:hibernate-validator:5.1.1.Final'
        compile 'joda-time:joda-time:2.3'
        compile 'com.google.guava:guava:17.0'
        compile 'javax.mail:mail:1.4.7'
        compile 'org.freemarker:freemarker:2.3.20'
        compile 'commons-validator:commons-validator:1.4.0'
        compile 'com.google.code.gson:gson:2.2.4'
        compile 'commons-io:commons-io:2.4'
        compile 'org.apache.poi:poi:3.10-FINAL'
        compile 'commons-codec:commons-codec:1.9'
        compile 'commons-logging:commons-logging:1.1.3'
        compile 'com.googlecode.libphonenumber:libphonenumber:6.0'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.4.0'
        compile 'com.fasterxml.jackson.core:jackson-core:2.4.0'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.4.0'
        compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'

        compile 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:r232'
        compile 'javax.validation:validation-api:1.1.0.Final'
        compile 'org.hibernate:hibernate-validator:5.1.1.Final'
        compile 'commons-fileupload:commons-fileupload:1.3.1'
        compile 'commons-io:commons-io:2.4'
        compile 'javax.inject:javax.inject:1'
        compile 'org.apache.activemq:activemq-client:5.9.0'
        compile 'javax.mail:mail:1.4.7'
        compile 'org.quartz-scheduler:quartz:2.2.1'
        compile 'org.apache.commons:commons-lang3:3.3.2'

        compile 'org.apache.httpcomponents:httpclient:4.3.4'

        compile 'ch.qos.logback:logback-core:1.1.2'
        compile 'ch.qos.logback:logback-classic:1.1.2'
        compile 'net.logstash.logback:logstash-logback-encoder:2.7'
        compile 'net.bull.javamelody:javamelody-core:1.+'

        compile 'net.sf.uadetector:uadetector-core:0.9.16'
        compile 'net.sf.uadetector:uadetector-resources:2014.04'

        compile 'javax.validation:validation-api:1.1.0.Final'

        testCompile group: 'junit', name: 'junit', version: '4.+'
        testCompile 'org.mockito:mockito-all:1.9.5'
        testCompile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.46.0'
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = true
            html.enabled = false
        }
    }

    tasks.withType(JDepend) {
        reports {
            text.enabled = true
            xml.enabled = true
        }
    }

    tasks.withType(Pmd) {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }
}

project(':common') {
    jar.baseName = 'receiptofi-common'
}

project(':domain') {
    jar.baseName = 'receiptofi-domain'

    dependencies {
        compile project(':common')
    }
}

project(':service') {
    jar.baseName = 'receiptofi-service'

    dependencies {
        compile project(':common'), project(':domain')
    }
}

project(':security') {
    apply plugin: 'war'
    jar.baseName = 'receiptofi-security'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':common'), project(':domain')

        providedCompile 'javax.servlet:javax.servlet-api:3.1.+'
        runtime 'javax.servlet:jstl:1.+'
    }
}

project(':social') {
    apply plugin: 'war'
    jar.baseName = 'receiptofi-social'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':common'), project(':domain'), project(':service')

        providedCompile 'javax.servlet:javax.servlet-api:3.1.+'
        runtime 'javax.servlet:jstl:1.+'
    }
}

project(':web') {
    apply plugin: 'war'
    jar.baseName = 'receiptofi-web'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile project(':common'), project(':domain'), project(':security'), project(':social'), project(':service')

        providedCompile 'javax.servlet:javax.servlet-api:3.1.+'
        runtime 'javax.servlet:jstl:1.+'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task copyJars(type: Copy) {
    from 'common/build/libs'
    from 'domain/build/libs'
    from 'service/build/libs'
    from 'security/build/libs'
    from 'social/build/libs'
    from 'web/build/libs'
    into warLibDir
    include '**/*.jar'
}

task unzip(type: Copy, dependsOn: [':web:war']) {
    def zipFile = file('web/build/libs/web-0.9.0.war')

    from zipTree(zipFile)
    into unzipDir
}

task prepWar(dependsOn: [unzip, copyJars]) << {
    warClassesDir.mkdirs()
    warConfDir.mkdirs()

    copy {
        from 'web/web'
        into warDir
        exclude '**/*.classes'
    }

    copy {
        from file("web/environment/$env/$env" + ".properties")
        into warConfDir
    }

    copy {
        from 'web/resources/conf/application-messages.properties'
        from 'web/resources/conf/config.properties'
        from 'web/resources/conf/redis.properties'

        if (env == 'test' || env == 'prod') {
            if(passFile.exists()) {
                from '/Users/Shared/Jenkins/Documents/receiptofi-additional-files/pass.properties'
            } else {
                throw new Exception("Required pass file does not exists: $passFile.canonicalPath")
            }
        }

        if(env == 'dev') {
            println "Development environment, getting local pass file $passFileLocal.canonicalPath"
            if(passFileLocal.exists()) {
                from 'web/resources/conf/pass.properties'
            } else {
                throw new Exception("Required pass file does not exists: $passFileLocal.canonicalPath")
            }
        }

        into warConfDir
    }

    copy {
        from "web/environment/$env/logback.xml"
        from 'web/resources/conf/messages.properties'
        into warClassesDir
    }

    copy {
        from "web/environment/$env/receipt-servlet.xml"
        into warWebInfDir
    }

    copy {
        from 'web/web/static/images'
        into warWebInfClassesImagesDir
    }

    copy {
        from unzipLibDir
        into warLibDir
    }
}

task clean(type: Delete) {
    delete buildDir
}

task war(type: War, dependsOn: prepWar) {
    doFirst {
        if (env == 'test' || env == 'prod') {
            // This adds escape character to message.properties; if you would like to avoid updating file at runtime
            // then create a new file 'versionFile.name' in your favorite location and add the new file as below
            //
            // from(rootDir) {
            //      include versionFile.name
            // }

            ant.propertyfile(file: "$warDir/WEB-INF/classes/messages.properties") {
                entry(key: "build.version", value: System.getenv().BUILD_NUMBER?.toInteger())
            }

            baseName = "receipt-$env-" + System.getenv().BUILD_NUMBER?.toInteger()
        }

        if(env == 'dev') {
            ant.propertyfile(file: "$warDir/WEB-INF/classes/messages.properties") {
                entry(key: "build.version", value: 'dev')
            }

            baseName = "receipt-$env"
        }
    }

    from warDir
    version ""
    destinationDir = file("$buildDir/war/$env")
}

defaultTasks 'clean', 'jar', 'war'